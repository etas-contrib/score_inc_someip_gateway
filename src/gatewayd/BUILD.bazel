# *******************************************************************************
# Copyright (c) 2025 Contributors to the Eclipse Foundation
#
# See the NOTICE file(s) distributed with this work for additional
# information regarding copyright ownership.
#
# This program and the accompanying materials are made available under the
# terms of the Apache License Version 2.0 which is available at
# https://www.apache.org/licenses/LICENSE-2.0
#
# SPDX-License-Identifier: Apache-2.0
# *******************************************************************************

"""
This daemon is a gateway between the SCORE middleware and the SOME/IP communication stack.
"""

load("@bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
load("@flatbuffers//:build_defs.bzl", "flatbuffer_cc_library")
load("@rules_cc//cc:cc_binary.bzl", "cc_binary")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@score_communication//bazel/tools:json_schema_validator.bzl", "validate_json_schema_test")
load("@score_someip_gateway//bazel/tools:someip_config.bzl", "generate_someip_config_bin", "validate_someip_config_test")

exports_files(
    [
        "etc/gatewayd_config.fbs",
        "etc/gatewayd_config.json",
        "etc/gatewayd_config.schema.json",
    ],
)

cc_binary(
    name = "gatewayd",
    srcs = ["main.cpp"],
    args = [
        "-config_file",
        "$(rootpath :config_file)",
        "-service_instance_manifest",
        "$(rootpath etc/mw_com_config.json)",
    ],
    data = [
        "etc/mw_com_config.json",
        ":config_file",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":config_flatbuffers",
        ":local_service_instance",
        ":remote_service_instance",
        "//src/network_service:consumer",
        "@score_communication//score/mw/com",
    ],
)

cc_library(
    name = "local_service_instance",
    srcs = ["local_service_instance.cpp"],
    hdrs = ["local_service_instance.h"],
    deps = [
        ":config_flatbuffers",
        "//src/network_service:consumer",
        "@score_communication//score/mw/com",
    ],
)

cc_library(
    name = "remote_service_instance",
    srcs = ["remote_service_instance.cpp"],
    hdrs = ["remote_service_instance.h"],
    deps = [
        ":config_flatbuffers",
        "//src/network_service:consumer",
        "//tests/performance_benchmarks:echo_service",
        "@score_communication//score/mw/com",
    ],
)

flatbuffer_cc_library(
    name = "config_flatbuffers",
    srcs = ["etc/gatewayd_config.fbs"],
)

exports_files(
    ["etc/mw_com_config.json"],
    visibility = ["//deployment:__subpackages__"],
)

exports_files(
    ["etc/mw_com_config.json"],
    visibility = ["//deployment:__subpackages__"],
)

generate_someip_config_bin(
    name = "config_file",
    json = "etc/gatewayd_config.json",
    output = "etc/gatewayd_config.bin",
)

validate_json_schema_test(
    name = "mw_com_config_schema_valid",
    size = "small",
    json = "etc/mw_com_config.json",
    schema = "@score_communication//score/mw/com:config_schema",
    tags = ["lint"],
)

filegroup(
    name = "config_schema",
    srcs = ["etc/gatewayd_config.schema.json"],
    visibility = ["//visibility:public"],
)

genrule(
    name = "config_schema.generate",
    srcs = ["etc/gatewayd_config.fbs"],
    outs = ["gatewayd_config.schema.json"],
    cmd = "$(location @flatbuffers//:flatc) --jsonschema -o $(@D) $(SRCS)",
    tools = ["@flatbuffers//:flatc"],
    visibility = ["//visibility:public"],
)

write_source_files(
    name = "config_schema.update",
    files = {
        "etc/gatewayd_config.schema.json": ":config_schema.generate",
    },
)

native_binary(
    name = "gatewayd_example",
    src = "//src/gatewayd",
    args = [
        "-service_instance_manifest",
        "$(rootpath etc/mw_com_config.json)",
    ],
    data = [
        "etc/mw_com_config.json",
        ":config_file",
    ],
)

# *******************************************************************************
# Copyright (c) 2025 Contributors to the Eclipse Foundation
#
# See the NOTICE file(s) distributed with this work for additional
# information regarding copyright ownership.
#
# This program and the accompanying materials are made available under the
# terms of the Apache License Version 2.0 which is available at
# https://www.apache.org/licenses/LICENSE-2.0
#
# SPDX-License-Identifier: Apache-2.0
# *******************************************************************************

"S-CORE SOME/IP Gateway"

module(name = "score_someip_gateway")

# ============================================================================
# Python Configuration
# ============================================================================
bazel_dep(name = "rules_python", version = "1.4.1", dev_dependency = True)

# rules_python 1.5.0 breaks score_docs_as_code; pin to 1.4.1 until resolved.
single_version_override(
    module_name = "rules_python",
    version = "1.4.1",
)

# Python 3.12: Required for testing infrastructure and code generation tools
PYTHON_VERSION = "3.12"

python = use_extension(
    "@rules_python//python/extensions:python.bzl",
    "python",
    dev_dependency = True,
)
python.toolchain(
    is_default = True,
    python_version = PYTHON_VERSION,
)
use_repo(python)

bazel_dep(
    name = "score_bazel_tools_python",
    version = "0.1.3",
    dev_dependency = True,
    repo_name = "bazel_tools_python",
)

# Python pip dependencies for testing
pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip", dev_dependency = True)
pip.parse(
    hub_name = "someip_pip",
    python_version = PYTHON_VERSION,
    requirements_lock = "//:python_requirements_lock.txt",
)
use_repo(pip, "someip_pip")

# ============================================================================
# Testing & Performance Measurement
# ============================================================================
bazel_dep(name = "googletest", version = "1.17.0", dev_dependency = True)
bazel_dep(name = "google_benchmark", version = "1.9.4", dev_dependency = True)

# ============================================================================
# Build System Rules
# ============================================================================
bazel_dep(name = "rules_cc", version = "0.2.14")
bazel_dep(name = "rules_rust", version = "0.63.0")
bazel_dep(name = "rules_pkg", version = "1.1.0")

# Platforms: Standard constraint values and platform definitions
# Required for select() expressions that use @platforms//cpu:* and @platforms//os:*
bazel_dep(name = "platforms", version = "1.0.0")

# ============================================================================
# Toolchains & Platform Configuration
# ============================================================================
# S-CORE Platform Definitions: Provides constraint values and platform definitions
bazel_dep(name = "score_bazel_platforms", version = "0.0.4")

bazel_dep(name = "score_toolchains_rust", version = "0.7.0", dev_dependency = True)
git_override(
    module_name = "score_toolchains_rust",
    commit = "40abf2df405dc714472f72f0f8538f04a5396e0e",  # Last verified: 2026-02-10
    remote = "https://github.com/etas-contrib/score_toolchains_rust.git",
)

# ============================================================================
# QNX8 Toolchain Configuration
# ============================================================================
bazel_dep(name = "score_bazel_cpp_toolchains", version = "0.2.2", dev_dependency = True)
git_override(
    module_name = "score_bazel_cpp_toolchains",
    commit = "bef681a4bf751060d756d9ac21c2afb7981c9204",  # Last verified: 2026-02-09
    remote = "https://github.com/etas-contrib/score_bazel_cpp_toolchains.git",
)

gcc = use_extension("@score_bazel_cpp_toolchains//extensions:gcc.bzl", "gcc", dev_dependency = True)
gcc.toolchain(
    name = "score_gcc_x86_64_toolchain",
    target_cpu = "x86_64",
    target_os = "linux",
    use_default_package = True,
    version = "12.2.0",
)
gcc.toolchain(
    name = "score_gcc_aarch64_toolchain",
    target_cpu = "aarch64",
    target_os = "linux",
    use_default_package = True,
    version = "12.2.0",
)
gcc.toolchain(
    name = "score_qcc_x86_64_toolchain",
    sdp_version = "8.0.0",
    target_cpu = "x86_64",
    target_os = "qnx",
    use_default_package = True,
    version = "12.2.0",
)
gcc.toolchain(
    name = "score_qcc_aarch64_toolchain",
    sdp_version = "8.0.0",
    target_cpu = "aarch64",
    target_os = "qnx",
    use_default_package = True,
    version = "12.2.0",
)
use_repo(
    gcc,
    "score_gcc_aarch64_toolchain",
    "score_gcc_x86_64_toolchain",
    "score_qcc_aarch64_toolchain",
    "score_qcc_x86_64_toolchain",
)

register_toolchains(
    "@score_gcc_x86_64_toolchain//:all",
    "@score_gcc_aarch64_toolchain//:all",
    "@score_qcc_x86_64_toolchain//:all",
    "@score_qcc_aarch64_toolchain//:all",
    dev_dependency = True,
)

# ============================================================================
# LLVM C/C++ Toolchain Configuration
# ============================================================================

# LLVM Toolchain: C/C++ cross-compilation support for x86_64 and aarch64
# Integrates with LLVM 19.1.0 and Debian Bullseye sysroots
bazel_dep(name = "toolchains_llvm", version = "1.5.0", dev_dependency = True)

# Configures LLVM 19.1.0 for C/C++ compilation with cross-architecture support.
llvm = use_extension(
    "@toolchains_llvm//toolchain/extensions:llvm.bzl",
    "llvm",
    dev_dependency = True,
)
llvm.toolchain(
    name = "llvm_toolchain",
    cxx_standard = {"": "c++17"},
    llvm_version = "19.1.7",
)
use_repo(llvm, "llvm_toolchain")

register_toolchains(
    "@llvm_toolchain//:all",
    dev_dependency = True,
)

# ============================================================================
# Tooling & Development
# ============================================================================
bazel_dep(name = "score_tooling", version = "1.0.4", dev_dependency = True)
bazel_dep(name = "aspect_rules_lint", version = "1.10.2", dev_dependency = True)
bazel_dep(name = "buildifier_prebuilt", version = "8.2.0.2", dev_dependency = True)

# ============================================================================
# Documentation
# ============================================================================
bazel_dep(name = "score_docs_as_code", version = "2.3.0", dev_dependency = True)

# ============================================================================
# Communication Framework
# ============================================================================
bazel_dep(name = "score_communication", version = "0.1.2")

# ============================================================================
# Base Libraries
# ============================================================================
# Patch note: baselibs-multiarch.patch adds multi-arch support for .deb downloads.
# score_baselibs hardcodes amd64 URLs; patch replaces them with architecture-aware
# selection (x86_64 or aarch64) based on TARGET_ARCH environment variable.
bazel_dep(name = "score_baselibs", version = "0.2.2")
single_version_override(
    module_name = "score_baselibs",
    patch_strip = 1,
    patches = ["//:third_party/baselibs-multiarch.patch"],
)

# ============================================================================
# Code Generation - FlatBuffers
# ============================================================================
bazel_dep(name = "flatbuffers", version = "25.2.10")

# TRLC: Traceability Language Compiler for requirements management
git_override(
    module_name = "trlc",
    commit = "650b51a47264a4f232b3341f473527710fc32669",  # trlc-2.0.2 release
    remote = "https://github.com/bmw-software-engineering/trlc.git",
)

# ============================================================================
# Examples: Car Window Simulator
# ============================================================================
car_window_sim_crate = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate", dev_dependency = True)
car_window_sim_crate.spec(
    package = "anyhow",
    version = "1",
)
car_window_sim_crate.spec(
    package = "async-trait",
    version = "0.1",
)
car_window_sim_crate.from_specs(name = "car_window_sim_crate")
use_repo(car_window_sim_crate, "car_window_sim_crate")

# ============================================================================
# SOME/IP Stack Integration
# ============================================================================
# Rules for Foreign/C/C++/CMake integration
bazel_dep(name = "rules_foreign_cc", version = "0.15.1")
single_version_override(
    module_name = "rules_foreign_cc",
    patch_strip = 1,
    patches = [
        "//:third_party/rules_foreign_cc-dont_block_network.patch",
        "//:third_party/rules_foreign_cc-fix_proc_self_cwd.patch",
    ],
)

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "boost",
    build_file_content = """
filegroup(
    name = "all_srcs",
    srcs = glob(["**"], exclude = ["libs/**/test/**"]),
    visibility = ["//visibility:public"],
)
""",
    patch_strip = 1,
    patches = ["//:third_party/boost-asio_qnx.patch"],
    sha256 = "7da75f171837577a52bbf217e17f8ea576c7c246e4594d617bfde7fafd408be5",
    strip_prefix = "boost-1.87.0",
    urls = ["https://github.com/boostorg/boost/releases/download/boost-1.87.0/boost-1.87.0-cmake.tar.xz"],
)

http_archive(
    name = "vsomeip",
    build_file = "@//:third_party/vsomeip.BUILD.bazel",
    integrity = "sha256-hMjkJs3fQ14MVtpwupfno/UbK+rxV+1zih9632bO5Sk=",
    patch_strip = 1,
    patches = ["//:third_party/vsomeip-qnx8.patch"],
    strip_prefix = "vsomeip-3.6.1",
    urls = [
        "https://github.com/COVESA/vsomeip/archive/refs/tags/3.6.1.tar.gz",
    ],
)

# ============================================================================
# JSON Schema generation, validation and handling (e.g. write_source_files)
# ============================================================================
bazel_dep(name = "bazel_lib", version = "3.1.0", dev_dependency = True)

bazel_dep(name = "bazel_skylib", version = "1.9.0")
